// Scilab code used to generate files containing keywords
function generateYaml(names, filename)
    // sort all names
    builtins = gsort(names, "g", "i");
    // add quotes to avoid issues with some characters such as %, #, ...
    builtins = "''" + builtins + "''";
    // add " - " at the beginning of eash line
    builtins = " - " + builtins
    // add Scilab version as a comment
    builtins = ["# *** Generated by " + getversion() + " ***"; "# *** DO NOT MODIFY ***"; builtins]
    // write output file
    filepath = get_absolute_file_path("scilab.sce")
    mputl(builtins, fullfile(filepath, filename))
endfunction

// get all Scilab keywords names
allkeywords = getscilabkeywords();
primitives = allkeywords(1); // C++/fortran functions
commands = allkeywords(2); // Keywords
predefinedvariables = allkeywords(3); // Predefined variables
scilabfunctions = allkeywords(4); // Scilab macros (functions written in Scilab language)
xcosfunctions = allkeywords(5); // Scilab macros (functions written in Scilab language)

// generate files for Rouge
generateYaml([primitives], "builtins.yml")
generateYaml([commands], "keywords.yml")
generateYaml([predefinedvariables; "%T"; "%t"; "%F"; "%f"], "predefs.yml")
generateYaml([scilabfunctions; xcosfunctions], "functions.yml")
